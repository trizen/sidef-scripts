#!/usr/bin/ruby

# Daniel "Trizen" È˜uteu
# License: GPLv3
# Date: 08 November 2016
# Website: https://github.com/trizen

#
## A basic Rational() class, implementing some common fraction identities.
#

class Rational(num, den) {

    method +(Number o) {
        self + Rational(o, 1)
    }

    method +(Rational o) {
        Rational(
            num*o.den + o.num*den,
            den*o.den
        )
    }

    method -(Number o) {
        self + -o
    }

    method -(Rational o) {
        self + -o
    }

    method *(Number o) {
        Rational(num*o, den)
    }

    method *(Rational o) {
        Rational(num*o.num, den*o.den)
    }

    method /(Number o) {
        Rational(
            num,
            den * o
        )
    }

    method /(Rational o) {
        Rational(
            num * o.den,
            den * o.num,
        )
    }

    method **(Number o) {
        if (o < 0) {
            var a = o.abs
            Rational(den**a, num**a)
        }
        else {
            Rational(num**o, den**o)
        }
    }

    method neg {
        Rational(-num, den)
    }

    method to_s {
        "Rational(#{num}, #{den})"
    }
}

class Number {
    method +(Rational o) {
        o + self
    }

    method -(Rational o) {
        -o + self
    }

    method *(Rational o) {
        o * self
    }

    method /(Rational o) {
        o**(-1) * self
    }
}

var r = Rational(1, 1)

prime(10).primes_each {|p|
    r *= (1 - Rational(1, p))
    say r
}
