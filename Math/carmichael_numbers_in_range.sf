#!/usr/bin/ruby

# Daniel "Trizen" È˜uteu
# Date: 29 August 2022
# https://github.com/trizen

# Generate all the Carmichael numbers with n prime factors in a given range [a,b]. (not in sorted order)

# See also:
#   https://en.wikipedia.org/wiki/Almost_prime
#   https://trizenx.blogspot.com/2020/08/pseudoprimes-construction-methods-and.html

# PARI/GP program:
#   carmichael(A, B, k) = A=max(A, vecprod(primes(k+1))\2); (f(m, l, p, k, u=0, v=0) = my(list=List()); if(k==1, forprime(p=u, v, my(t=m*p); if((t-1)%l == 0 && (t-1)%(p-1) == 0, listput(list, t))), forprime(q = p, sqrtnint(B\m, k), my(t = m*q); my(L=lcm(l, q-1)); if(gcd(L, t) == 1, my(u=ceil(A/t), v=B\t); if(u <= v, my(r=nextprime(q+1)); if(k==2 && r>u, u=r); list=concat(list, f(t, L, r, k-1, u, v)))))); list); vecsort(Vec(f(1, 1, 3, k)));

func carmichael_numbers_in_range(a, b, k, callback) {

    a = max(pn_primorial(k+1)/2, a)

    func (m, lambda, p, k) {

        var y = idiv(b,m).iroot(k)

        if (k == 1) {

            var x = max(p, idiv_ceil(a, m))

            each_prime(x, y, {|p|
                with (m*p - 1) {|t|
                    if ((lambda `divides` t) && (p-1 `divides` t)) {
                        callback(t+1)
                    }
                }
            })

            return nil
        }

        for(var r; p <= y; p = r) {

            r = p.next_prime

            var t = m*p
            var u = idiv_ceil(a, t)
            var v = idiv(b, t)

            if (u <= v) {
                var L = lcm(lambda, p-1)
                if (t.is_coprime(L)) {
                    __FUNC__(t, L, r, k-1)
                }
            }
        }
    }(1, 1, 3, k)

    return callback
}

# High-level implementation (unoptimized):
func carmichael_numbers_in_range_unoptimized(A, B, k, callback) {

    func F(m, lambda, p, k) {
        if (k == 1) {
            each_prime(max(p, ceil(A/m)), floor(B/m), {|q|
                if (lcm(lambda, q-1) `divides` (m*q - 1)) {
                    callback(m*q)
                }
            })
        }
        elsif (k >= 2) {
            for q in (primes(p, (B/m)**(1/k))) {
                if (ceil(A/(m*q)) <= floor(B/(m*q))) {
                    if (gcd(lcm(lambda, q-1), m*q) == 1) {
                        F(m*q, lcm(lambda, q-1), q.next_prime, k-1)
                    }
                }
            }
        }
    }

    F(1, 1, 3, k)
}

# Generate all the 5-Carmichael numbers in the range [100, 2*10^7]

var k    = 5
var from = 100
var upto = 2e7

say gather { carmichael_numbers_in_range(from, upto, k, { take(_) }) }.sort
say gather { carmichael_numbers_in_range_unoptimized(from, upto, k, { take(_) }) }.sort

__END__
[825265, 1050985, 9890881, 10877581, 12945745, 13992265, 16778881, 18162001]
