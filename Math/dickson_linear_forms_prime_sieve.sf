#!/usr/bin/ruby

# Sieve for Dickson primes: primes of the form `m*k + a`, for `k = 1..n` and fixed `a`.
# Inspired by the PARI program by David A. Corneth from OEIS A372238.

# See also:
#   https://oeis.org/A088250
#   https://oeis.org/A318646
#   https://oeis.org/A372238/a372238.gp.txt
#   https://en.wikipedia.org/wiki/Dickson%27s_conjecture

func isrem(m, p, terms, alpha) {

    terms.each {|k|
        if (k*m + alpha -> is_div(p)) {
            return false
        }
    }

    return true
}

func remaindersmodp(p, terms, alpha) {
    ^p -> grep {|k| isrem(k, p, terms, alpha) }
}

func remainders_for_primes(primes, terms, alpha) {

    var res = [[0, 1]]

    primes.each {|p|

        var rems = remaindersmodp(p, terms, alpha)

        if (rems.is_empty) {
            rems = [0]
        }

        var nres = []
        res.each {|r|
            rems.each {|rem|
                nres << [Math.chinese(r, [rem, p]), lcm(p, r[1])]
            }
        }

        res = nres
    }

    res.map { .head }.sort
}

func linear_form_primes(terms, alpha = 1, callback) {

    var primes = terms.len.prime.primes

    var r = remainders_for_primes(primes, terms, alpha)
    var d = r.diffs
    var s = primes.prod

    while (d[0] == 0) {
        d.shift
    }

    d << (r[0] + s - r[-1])

    var m      = r[0]
    var d_len  = d.len

    for j in (0..Inf) {

        if (terms.all {|k| is_prime(k*m + alpha) }) {
            callback(m)
        }

        m += d[j % d_len]
    }
}

say "=> a(n) is the smallest number k such that r*k + 1 is prime for all r = 1 to n"

for n in (4..8) {

    var terms = @(1..n)
    var alpha = 1
    var m = nil

    linear_form_primes(terms, alpha, {|k|
        m = k
        break
    })

    say "a(#{n}) = #{m}"
}

say "\n=> The least Chernick's \"universal form\" Carmichael number with n prime factors"

for n in (3..8) {

    var terms = [6, 12, 1 .. n-2 -> map{|k| 9 * 2**k }...]
    var alpha = 1
    var m = nil

    linear_form_primes(terms, alpha, {|k|
        if (k.valuation(2) >= n-4) {
            m = k
            break
        }
    })

    say "a(#{n}) = #{m}"
    say terms.map {|b| m*b + alpha }.prod
}

__END__
=> a(n) is the smallest number k such that r*k + 1 is prime for all r = 1 to n
a(4) = 330
a(5) = 10830
a(6) = 25410
a(7) = 512820
a(8) = 512820

=> The least Chernick's "universal form" Carmichael number with n prime factors
a(3) = 1
1729
a(4) = 45
192739365541
a(5) = 380
26641259752490421121
a(6) = 380
1457836374916028334162241
a(7) = 780320
24541683183872873851606952966798288052977151461406721
a(8) = 950560
53487697914261966820654105730041031613370337776541835775672321
