#!/usr/bin/ruby

# Daniel "Trizen" È˜uteu
# Date: 14 February 2021
# https://github.com/trizen

# Generate k-almost prime numbers <= n. (not in sorted order)

# See also:
#   https://en.wikipedia.org/wiki/Almost_prime

func almost_prime_numbers(n, k, callback) {

    if (k == 1) {
        return n.each_prime(callback)
    }

    func (m, p, r) {

        var s = idiv(n,m).iroot(r)

        if (r == 2) {

            each_prime(p, s, {|r|
                var u = m*r
                each_prime(r, idiv(n, u), {|q|
                    callback(u*q)
                })
            })

            return nil
        }

        each_prime(p, s, {|q|
            __FUNC__(m*q, q, r-1)
        })
    }(1, 2, k)

    return callback
}

var n = 100
var k = 4

say gather {
    almost_prime_numbers(n, k, { take(_) })
}

__END__
[16, 24, 40, 56, 88, 36, 60, 84, 100, 54, 90, 81]
