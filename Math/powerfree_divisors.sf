#!/usr/bin/ruby

# Author: Trizen
# Date: 05 October 2021
# https://github.com/trizen

# Generate the k-powerfree divisors of n.

func powerfree_divisors (n, k=2) {

    var d  = [1]
    var pp = n.factor_exp

    for p,e in (pp) {

        e = min(e, k-1)

        var r = 1
        d << gather {
            e.times {
                r *= p
                d.each { |u|
                    take(u*r)
                }
            }
        }...
    }

    d.sort
}

say powerfree_divisors(5040, 2)
say powerfree_divisors(5040, 3)

__END__
[1, 2, 3, 5, 6, 7, 10, 14, 15, 21, 30, 35, 42, 70, 105, 210]
[1, 2, 3, 4, 5, 6, 7, 9, 10, 12, 14, 15, 18, 20, 21, 28, 30, 35, 36, 42, 45, 60, 63, 70, 84, 90, 105, 126, 140, 180, 210, 252, 315, 420, 630, 1260]
