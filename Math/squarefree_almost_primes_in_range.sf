#!/usr/bin/ruby

# Daniel "Trizen" È˜uteu
# Date: 06 March 2021
# Edit: 13 March 2021
# https://github.com/trizen

# Generate squarefree k-almost primes in range [a,b]. (not in sorted order)

# See also:
#   https://en.wikipedia.org/wiki/Almost_prime

func squarefree_almost_primes(a, b, k, callback) {

    a = max(k.pn_primorial, a)

    func (m, p, k) {

        var y = idiv(b,m).iroot(k)

        if (k == 1) {

            var x = max(p, idiv_ceil(a, m))

            each_prime(x, y, {|p|
                callback(m*p) if !(p `divides` m)
            })

            return nil
        }

        each_prime(p, y, {|p|

            var t = m*p
            var u = idiv_ceil(a, t)
            var v = idiv(b, t)

            next if (u > v)    # optimization for tight ranges

            __FUNC__(t, p, k-1) if !(p `divides` m)
        })
    }(1, 2, k)

    return callback
}

# Generate squarefree 5-almost in the range [3000, 10000]

var k    = 5
var from = 3000
var upto = 10000

var arr = gather {
    squarefree_almost_primes(from, upto, k, { take(_) })
}.sort

assert_eq(arr, max(from, k.pn_primorial)..upto -> grep{.is_squarefree && .is_almost_prime(k)})     # just for testing
say arr

# Run some more tests

for k in (1..10) {

    var from = k.pn_primorial
    var upto = from+1e4.irand

    say "Testing: k = #{k} with #{from} .. #{upto}"

    var arr = gather {
        squarefree_almost_primes(from, upto, k, { take(_) })
    }.sort

    assert_eq(arr, max(from, k.pn_primorial)..upto -> grep{.is_squarefree && .is_almost_prime(k)})
}
