#!/usr/bin/ruby

# Daniel "Trizen" È˜uteu
# Date: 26 February 2018
# https://github.com/trizen

# Compute the simple continued fraction expansion of a given positive number, using the Stern-Brocot encoding tree.

# See also:
#   https://www.youtube.com/watch?v=qPeD87HJ0UA
#   https://en.wikipedia.org/wiki/Stern%E2%80%93Brocot_tree

func stern_brocot_encode (Number r) {
    var (m, n) = r.abs.rat_approx.nude

    var L = 0
    var R = 0

    var e = []

    while (m != n) {
        if (m < n) {
            if (R != 0) {
                e << R
                R = 0
            }
            L += 1
            n -= m
        }
        else {
            if (L != 0) {
                e << L
                L = 0
            }

            R += 1
            m -= n
        }
    }

    return e
}

say stern_brocot_encode(Num.pi)     #=> [3, 7, 15, 1, 292, 1, 1, 1, 2, 1, 3, 1, 14, 2, ... ]
