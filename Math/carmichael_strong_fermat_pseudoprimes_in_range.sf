#!/usr/bin/ruby

# Daniel "Trizen" È˜uteu
# Date: 07 November 2022
# https://github.com/trizen

# Generate all the Carmichael numbers with n prime factors in a given range [A,B] that are also strong Fermat pseudoprimes to a given base. (not in sorted order)

# See also:
#   https://en.wikipedia.org/wiki/Almost_prime
#   https://trizenx.blogspot.com/2020/08/pseudoprimes-construction-methods-and.html

# PARI/GP program:
#   carmichael_strong_psp(A, B, k, base) = A=max(A, vecprod(primes(k+1))\2); (f(m, l, p, k, k_exp, congr, u=0, v=0) = my(list=List()); if(k==1, forprime(q=u, v, my(t=m*q); if((t-1)%l == 0 && (t-1)%(q-1) == 0, my(tv=valuation(q-1, 2)); if(tv > k_exp && Mod(base, q)^(((q-1)>>tv)<<k_exp) == congr, listput(list, t)))), forprime(q = p, sqrtnint(B\m, k), if(base%q != 0, my(tv=valuation(q-1, 2)); if(tv > k_exp && Mod(base, q)^(((q-1)>>tv)<<k_exp) == congr, my(L=lcm(l, q-1)); if(gcd(L, m) == 1, my(t = m*q, u=ceil(A/t), v=B\t); if(u <= v, my(r=nextprime(q+1)); if(k==2 && r>u, u=r); list=concat(list, f(t, L, r, k-1, k_exp, congr, u, v)))))))); list); my(res=f(1, 1, 3, k, 0, 1)); for(v=0, logint(B, 2), res=concat(res, f(1, 1, 3, k, v, -1))); vecsort(Vec(res));

func carmichael_strong_fermat_in_range(A, B, k, base, callback) {

    A = max(k.pn_primorial, A)

    var generator = func (m, lambda, p, k, k_exp, congr) {

        var y = idiv(B,m).iroot(k)

        if (k == 1) {

            var x = max(p, idiv_ceil(A, m))

            each_prime(x, y, {|p|
                with (m*p - 1) {|t|
                    if ((lambda `divides` t) && (p.dec `divides` t)) {
                        var v = p.dec.valuation(2)
                        if (v > k_exp && powmod(base, (p.dec>>v)<<k_exp, p).is_congruent(congr, p)) {
                            callback(t+1)
                        }
                    }
                }
            })

            return nil
        }

        for(var r; p <= y; p = r) {

            r = p.next_prime
            p.divides(base) && next

            var val2 = p.dec.valuation(2)
            val2 > k_exp || next
            powmod(base, (p.dec>>val2)<<k_exp, p).is_congruent(congr, p) || next

            var L = lcm(lambda, p-1)
            m.is_coprime(L) || next

            var t = m*p
            var u = idiv_ceil(A, t)
            var v = idiv(B, t)

            if (u <= v) {
                __FUNC__(t, L, r, k-1, k_exp, congr)
            }
        }
    }

    # Case where 2^d == 1 (mod p), where d is the odd part of p-1.
    generator(1, 1, 2, k, 0, 1)

    # Cases where 2^(d * 2^v) == -1 (mod p), for some v >= 0.
    for v in (0..B.ilog2) {
        generator(1, 1, 2, k, v, -1)
    }

    return callback
}

# Generate all the 3-Carmichael numbers in the range [1, 10^8] that are also strong pseudoprimes to base 2.

var k    = 3
var base = 2
var from = 1
var upto = 1e6

say gather { carmichael_strong_fermat_in_range(from, upto, k, base, { take(_) }) }.sort

__END__
[15841, 29341, 52633, 252601, 314821]
