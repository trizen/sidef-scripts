#!/usr/bin/ruby

# Daniel "Trizen" È˜uteu
# License: GPLv3
# Date: 12 January 2016
# https://github.com/trizen

# A simple example for the RSA algorithm.

require('ntheory')

var p = %S<ntheory>.random_strong_prime(2048)
var q = %S<ntheory>.random_strong_prime(2048)

var n = p*q
var phi = (p-1)*(q-1)

func gcd(u,v) {
    while (v) {
        (u, v) = (v, u % v)
    }
    return abs(u)
}

var e = 0
for (var k = 16 ; gcd(e, phi) != 1 ; ++k) {
    e = (2**k + 1)
}

func invmod(a,n) {
    var (t, nt, r, nr) = (0, 1, n, a % n)
    while (nr) {
        var quot = int((r - (r % nr)) / nr)
        (nt, t) = (t - quot*nt, nt)
        (nr, r) = (r - quot*nr, nr)
    }
    r > 1 && return nil
    t < 0 && (t += n)
    return t
}

var d = invmod(e, phi)

func expmod(a,b,n) {
    var c = 1
    do {
        (c *= a) %= n if b.is_odd
        (a *= a) %= n
    } while (b >>= 1)
    return c
}

var m = 123456789
var c = expmod(m, e, n)
var M = expmod(c, d, n)
say M

assert_eq(M, m)
