#!/usr/bin/ruby

# A simple implementation of the continued fraction factorization method,
# combined with modular arithmetic (variation of the Brillhart-Morrison algorithm).

# Similar to solving the Pell equation:
#   x^2 - d*y^2 = 1, where `d` is known.

# See also:
#   https://en.wikipedia.org/wiki/Pell%27s_equation
#   https://en.wikipedia.org/wiki/Continued_fraction_factorization

# Parts of code inspired by:
#    https://github.com/martani/Quadratic-Sieve

func gauss_elimination(A, n) {

    var m = A.end
    var I = (m+1).of {|i| 1 << i }

    var nrow = -1

    for col in (0 .. min(m, n)) {
        var npivot = -1

        for row in (nrow+1 .. m) {
            if (A[row].bit(col)) {
                npivot = row
                nrow++
                break
            }
        }

        next if (npivot < 0)

        if (npivot != nrow) {
            A.swap(npivot, nrow)
            I.swap(npivot, nrow)
        }

        for row in (nrow+1 .. m) {
            if (A[row].bit(col)) {
                A[row] ^= A[nrow]
                I[row] ^= I[nrow]
            }
        }
    }

    return I
}

func check_factor (n, g, factors) {

    while (g `divides` n) {

        n /= g;
        factors << g

        if (is_prime(n)) {
            factors << n
            return 1
        }
    }

    return n
}

func cffmm (n) {

    # Check for primes and negative numbers
    return []  if (n <= 1)
    return [n] if n.is_prime

    # Check for perfect powers
    if (n.is_power) {

        var root  = n.perfect_root
        var power = n.perfect_power

        var factors = __FUNC__(root)

        return (factors * power -> sort)
    }

    # Check for divisibility by 2
    if (n.is_even) {

        var v = n.valuation(2)
        var t = n>>v

        var factors = v.of(2)

        if (t > 1) {
            factors += __FUNC__(t)
        }

        return factors
    }

    var resolve_factor = {|a,b|
        var g = gcd(a - b.isqrt, n)

        if ((g > 1) && (g < n)) {
            return (
                __FUNC__(g) +
                __FUNC__(n/g) -> sort
            )
        }
    }

    var x = n.isqrt
    var y = x
    var z = 1
    var w = x+x
    var r = w

    var (e1, e2) = (1, 0)
    var (f1, f2) = (0, 1)

    var B = int(exp(sqrt(log(n) * log(log(n))) / 2))        # B-smooth limit

    var factor_base  = B.primes.grep {|p| (p <= 97) || (kronecker(n, p) == 1) }
    var factor_prod  = factor_base.prod
    var factor_index = Hash(factor_base ~Z ^factor_base -> flat...)

    var L = factor_base.len+1

    var Q = []
    var A = []

    func exponent_signature(factors) {
        var sig = 0

        for p,e in factors {
            sig.setbit!(factor_index{p}) if e.is_odd
        }

        return sig
    }

    do {
        y = (r*z - y)
        z = ((n - y*y) // z)        #/
        r = round((x + y) / z)

        var a = ((x*f2 + e2) % n)
        var b = (a*a % n)
        var c = (b > w ? n-b : b)

        if (c.is_square) {
            resolve_factor(a, c)
        }

        if (c.is_smooth_over_prod(factor_prod)) {
            var c_factors = c.factor_exp

            if (c_factors) {
                A << exponent_signature(c_factors)
                Q << [a, c]
            }
        }

        (f1, f2) = (f2, (r*f2 + f1) % n)
        (e1, e2) = (e2, (r*e2 + e1) % n)

    } while ((z != 1) && (A.len <= L))

    if (A.len < L) {
        A += (L - A.end + 1 -> of(0))
    }

    var I  = gauss_elimination(A, L-1)
    var LR = (A.end - A.rindex_by { !.is_zero })

    var rem     = n
    var factors = []

    for solution in (I.last(LR)) {

        var X = 1
        var Y = 1

        var done = false

        for i in (^Q) {

            if (solution.bit(i)) {

                X *= Q[i][0] %= n      #=
                Y *= Q[i][1]

                var g = gcd(X - Y.isqrt, rem)

                if ((g > 1) && (g < rem)) {
                    rem = check_factor(rem, g, factors)
                    if (rem == 1) { done = true; break }
                }
            }
        }

        break if done
    }

    var final_factors = []

    for f in (factors) {
        if (f.is_prime) {
            final_factors << f
        }
        else {
            final_factors << __FUNC__(f)...
        }
    }

    if (rem != 1) {
        if (rem != n) {
            final_factors << __FUNC__(rem)...
        }
        else {
            final_factors << rem
        }
    }

    final_factors.sort
}

var composites = (ARGV ? ARGV.map{.to_i} : {|k| irand(2, 1<<k) }.map(2..60) )

for n in (composites) {

    var factors = cffmm(n)
    assert_eq(factors.prod, n)

    if (factors.all { .is_prime }) {
        say "#{n} = #{factors.join(' * ')}"
    }
    else {
        say "#{n} = #{factors.join(' * ')} (incomplete factorization)"
    }
}
