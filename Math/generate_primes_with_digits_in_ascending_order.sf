#!/usr/bin/ruby

# Daniel "Trizen" È˜uteu
# Date: 08 April 2022
# https://github.com/trizen

# Generate prime numbers that have all digits in ascending order.

# See also:
#   https://rosettacode.org/wiki/Ascending_primes

# OEIS sequences:
#   https://oeis.org/A052015 -- Primes with distinct digits in ascending order.

func primes_with_ascending_digits(base = 10) {

    var list = []
    var digits = @(1..^base -> flip)

    var end_digits = digits.grep { .is_coprime(base) }
    list << digits.grep { .is_prime && !.is_coprime(base) }...

    for k in (0 .. digits.end) {
        digits.combinations(k, {|*a|
            var v = a.digits2num(base)
            end_digits.each {|d|
                var n = (v*base + d)
                next if ((n >= base) && (a[0] >= d))
                list << n if (n.is_prime)
            }
        })
    }

    list.sort
}

var arr = primes_with_ascending_digits()

say "There are #{arr.len} ascending primes.\n"

arr.each_slice(10, {|*a|
    say a.map { '%8s' % _ }.join(' ')
})
