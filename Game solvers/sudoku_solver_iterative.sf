#!/usr/bin/ruby

# Author: Trizen
# Date: 12 February 2024
# https://github.com/trizen

# Fast algorithm to solve a Sudoku puzzle (iterative solution).

func is_valid(board, row, col, num) {

    # Check if the number is not present in the current row and column
    for i in ^9 {
        if ((board[row][i] == num) || (board[i][col] == num)) {
            return false
        }
    }

    # Check if the number is not present in the current 3x3 subgrid
    var (start_row, start_col) = (3*idiv(row, 3), 3*idiv(col, 3))

    for i in ^3, j in ^3 {
        if (board[start_row + i][start_col + j] == num) {
            return false
        }
    }

    return true
}

func find_empty_locations(board) {

    var locations = []

    # Find all empty positions (cells with 0)
    for i in ^9, j in ^9 {
        if (board[i][j] == 0) {
            locations << [i, j]
        }
    }

    return locations
}

func solve_sudoku_stack(board) {    # fallback method

    var stack = [board]

    while (stack) {

        var current_board = stack.pop
        var empty_locations = find_empty_locations(current_board) || return current_board

        var (row, col) = empty_locations.shift...

        for num in (1..9) {
            if (is_valid(current_board, row, col, num)) {
                var new_board = current_board.map { .clone }
                new_board[row][col] = num
                stack << new_board
            }
        }
    }

    return nil
}

func solve_sudoku(board) {

    loop {
        var empty_locations = find_empty_locations(board) || break

        var found = false

        # Solve easy cases
        for i,j in empty_locations {
            var(count=0, value=0)
            for n in (1..9) {
                is_valid(board, i, j, n) || next
                break if (++count > 1)
                value = n
            }
            if (count == 1) {
                board[i][j] = value
                found ||= true
            }
        }

        next if found

        # Solve more complex cases
        var stats = []
        for i,j in empty_locations {
            stats[i][j] = (1..9 -> grep{|n| is_valid(board, i, j, n) })
        }

        var cols = []
        var rows = []

        for i,j in empty_locations {
            stats[i][j].each {|v|
                cols[j][v] := 0 ++
                rows[i][v] := 0 ++
            }
        }

        found = false

        for i,j in empty_locations {
            stats[i][j].each {|v|
                if (cols[j][v] == 1) {
                    board[i][j] = v
                    found ||= true
                }
                elsif (rows[i][v] == 1) {
                    board[i][j] = v
                    found ||= true
                }
            }
        }

        next if found

        return solve_sudoku_stack(board)
    }

    return board
}

# Example usage:
# Define the Sudoku puzzle as a 9x9 list with 0 representing empty cells
var sudoku_board = %n(
    2 0 0  0 7 0  0 0 3
    1 0 0  0 0 0  0 8 0
    0 0 4  2 0 9  0 0 5

    9 4 0  0 0 0  6 0 8
    0 0 0  8 0 0  0 9 0
    0 0 0  0 0 0  0 7 0

    7 2 1  9 0 8  0 6 0
    0 3 0  0 2 7  1 0 0
    4 0 0  0 0 3  0 0 0
).slices(9)

func display_grid(grid) {
    for i in ^grid {
        print "#{grid[i]} "
        print " "  if ( 3 -> divides(i+1))
        print "\n" if ( 9 -> divides(i+1))
        print "\n" if (27 -> divides(i+1))
    }
}

var solution = solve_sudoku(sudoku_board)

if (solution) {
    display_grid(solution.flat)
}
else {
    warn "No unique solution exists."
}
