#!/usr/bin/ruby

# https://rosettacode.org/wiki/huffman_coding

func walk(n, s, h) {
    if (n.has(:a)) {
        h{n{:a}} = s
        printf("%3s: %s\n", n{:a}, s)
        return nil
    }
    walk(n{:0}, s+'0', h)
    walk(n{:1}, s+'1', h)
}

func make_tree(bytes) {

    var nodes = bytes.freq.kv.map_2d { |b,f|
        Hash(a => b, freq => f)
    }

    var n = Hash()
    while (nodes.sort_by!{|h| h{:freq} }.len > 1) {
        n = Hash(:0 => nodes.shift, :1 => nodes.shift)
        n{:freq} = (n{:0}{:freq} + n{:1}{:freq})
        nodes << n
    }

    walk(n, '', n{:tree} = Hash())
    return n
}

func encode(bytes, t) {
    t = t{:tree}
    bytes.map { t{_} }.join
}

func decode (enc, tree) {
    var n = tree
    var out = []

    enc.each {|bit|
        n = n{bit}
        if (n.has(:a)) {
            out << n{:a}
            n = tree
        }
    }

    return out
}

var text  = "this is an example for huffman encoding"
var bytes = text.bytes
var tree  = make_tree(bytes)
var enc   = encode(bytes, tree)

say enc
say decode(enc, tree).decode('utf8')
